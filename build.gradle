/*
 * Copyright Â© 2014-2020 The Android Password Store Authors. All Rights Reserved.
 * SPDX-License-Identifier: GPL-3.0-only
 */
buildscript {
    apply from: rootProject.file('dependencies.gradle')
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath deps.gradle_plugin.android
        classpath deps.gradle_plugin.kotlin
    }
}

plugins {
    id 'com.github.ben-manes.versions' version '0.28.0'
}

subprojects {
    apply from: rootProject.file('dependencies.gradle')
    repositories {
        google()
        jcenter()
        maven {
            url 'https://jitpack.io'
        }
    }
    pluginManager.withPlugin('kotlin-android') {
        dependencies {
            implementation deps.kotlin.stdlib8
        }
    }
    if (name == 'app') {
        apply plugin: 'com.android.application'
    } else {
        apply plugin: 'com.android.library'
    }
    android {
        compileSdkVersion versions.compileSdk
        buildToolsVersion = versions.buildTools
        defaultConfig {
            minSdkVersion versions.minSdk
            targetSdkVersion versions.targetSdk
            testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        }
        compileOptions {
            sourceCompatibility = JavaVersion.VERSION_1_8
            targetCompatibility = JavaVersion.VERSION_1_8
        }
    }
    tasks.withType(JavaCompile) {
        options.compilerArgs << '-Xlint:unchecked'
        options.deprecation = true
    }
    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile) {
        kotlinOptions {
            jvmTarget = '1.8'
            freeCompilerArgs += "-Xopt-in=kotlin.RequiresOptIn"
        }
    }
}

tasks.wrapper {
    gradleVersion = "6.4"
    distributionType = Wrapper.DistributionType.ALL
    distributionSha256Sum = "d08f7e24d061910382c2fda9915e6ed42dd1480ae2e99211f92c70190cb697e0"
}
