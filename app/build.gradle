apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'eclipse'

android {
    compileSdkVersion 27
    buildToolsVersion '27.0.3'
    defaultConfig {
        applicationId "com.zeapo.pwdstore"
        minSdkVersion 16
        targetSdkVersion 27
        versionCode 95
        versionName "1.2.0.75"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    lintOptions {
        abortOnError true // make sure build failes with lint errors!
        disable 'MissingTranslation', 'PluralsCandidate'
    }
    packagingOptions {
        exclude '.readme'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }
    //
    // To sign release builds, create the file `gradle.properties` in
    // $HOME/.gradle or in your project directory with this content:
    //
    // mStoreFile=/path/to/key.store
    // mStorePassword=xxx
    // mKeyAlias=alias
    // mKeyPassword=xxx
    //
    if (project.hasProperty('mStoreFile') &&
            project.hasProperty('mStorePassword') &&
            project.hasProperty('mKeyAlias') &&
            project.hasProperty('mKeyPassword')) {
        signingConfigs {
            release {
                storeFile file(mStoreFile)
                storePassword mStorePassword
                keyAlias mKeyAlias
                keyPassword mKeyPassword
            }
        }
        buildTypes.release.signingConfig = signingConfigs.release
    } else {
        buildTypes.release.signingConfig = null
    }
}

def daggerVersion = '2.16'

dependencies {
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.2'
// Because RxAndroid releases are few and far between, it is recommended you also
// explicitly depend on RxJava's latest version for bug fixes and new features.
// (see https://github.com/ReactiveX/RxJava/releases for latest 2.x.x version)
    implementation 'io.reactivex.rxjava2:rxjava:2.1.13'
    implementation "com.google.dagger:dagger:${daggerVersion}"
    implementation "com.google.dagger:dagger-android-support:${daggerVersion}"
    annotationProcessor "com.google.dagger:dagger-compiler:${daggerVersion}"
    annotationProcessor "com.google.dagger:dagger-android-processor:${daggerVersion}"
    testAnnotationProcessor "com.google.dagger:dagger-compiler:${daggerVersion}"
    testAnnotationProcessor "com.google.dagger:dagger-android-processor:${daggerVersion}"
    compile 'com.android.support:appcompat-v7:27.1.0'
    compile 'com.android.support:recyclerview-v7:27.1.0'
    compile 'com.android.support:cardview-v7:27.1.0'
    compile 'com.android.support:design:27.1.0'
    compile 'com.android.support:support-annotations:27.1.0'
    compile 'org.sufficientlysecure:openpgp-api:11.0'
    compile 'com.nononsenseapps:filepicker:2.4.2'
    compile('org.eclipse.jgit:org.eclipse.jgit:3.7.1.201504261725-r') {
        exclude group: 'org.apache.httpcomponents', module: 'httpclient'
    }
    compile 'com.jcraft:jsch:0.1.54'
    compile group: 'commons-io', name: 'commons-io', version: '2.4'
    compile group: 'commons-codec', name: 'commons-codec', version: '1.11'
    compile 'com.jayway.android.robotium:robotium-solo:5.3.1'
    compile "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"
    compile 'com.android.support.constraint:constraint-layout:1.0.2'
    implementation 'com.jakewharton:butterknife:8.8.1'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.8.1'

    // Testing-only dependencies
    androidTestCompile 'junit:junit:4.12'
    androidTestCompile 'org.mockito:mockito-core:2.8.47'
    androidTestCompile 'com.android.support.test:runner:1.0.1'
    androidTestCompile 'com.android.support.test:rules:1.0.1'
    androidTestCompile 'com.android.support.test.espresso:espresso-core:3.0.1'
    androidTestCompile 'com.android.support.test.espresso:espresso-intents:3.0.1'


}
repositories {
    mavenCentral()

    // temp. solution until we use use gradle 4.0
    maven { url 'https://maven.google.com' }
}
