apply plugin: 'com.android.application'
apply from: 'copyLibs.gradle'    // enable 'copyLibs.gradle' script plugin
apply plugin: 'eclipse'

android {
    compileSdkVersion 25
    buildToolsVersion '25.0.2'
    defaultConfig {
        applicationId "com.zeapo.pwdstore"
        minSdkVersion 15
        targetSdkVersion 25
        versionCode 78
        versionName "1.2.0.58"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    // avoid Travis failures
    lintOptions {
        abortOnError false
    }

    packagingOptions {
        exclude '.readme'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }

    //
    // To sign release builds, create the file `gradle.properties` in
    // $HOME/.gradle or in your project directory with this content:
    //
    // mStoreFile=/path/to/key.store
    // mStorePassword=xxx
    // mKeyAlias=alias
    // mKeyPassword=xxx
    //
    if (project.hasProperty('mStoreFile') &&
            project.hasProperty('mStorePassword') &&
            project.hasProperty('mKeyAlias') &&
            project.hasProperty('mKeyPassword')) {
        signingConfigs {
            release {
                storeFile file(mStoreFile)
                    storePassword mStorePassword
                    keyAlias mKeyAlias
                    keyPassword mKeyPassword
            }
        }
        buildTypes.release.signingConfig = signingConfigs.release
    } else {
        buildTypes.release.signingConfig = null
    }

}

dependencies {
    compile 'com.android.support:appcompat-v7:25.3.1'
    compile 'com.android.support:recyclerview-v7:25.3.1'
    compile 'com.android.support:cardview-v7:25.3.1'
    compile 'com.android.support:design:25.3.1'
    compile 'org.sufficientlysecure:openpgp-api:11.0'
    compile 'com.nononsenseapps:filepicker:2.4.2'
    compile('org.eclipse.jgit:org.eclipse.jgit:3.7.1.201504261725-r') {
        exclude group: 'org.apache.httpcomponents', module: 'httpclient'
    }
    compile 'com.jcraft:jsch:0.1.53'
    compile 'org.apache.commons:commons-io:1.3.2'
    compile 'com.jayway.android.robotium:robotium-solo:5.3.1'
    compile 'com.google.guava:guava:18.0'
}
tasks.findAll {    // make all tasks whose name starts with 'assemble'...
    it.name.startsWith 'assemble'
}.each {           // ... depending on 'copyDependenciesIntoLibs' task from 'copyLibs.gradle' script plugin
    it.dependsOn copyDependenciesIntoLibs
}
